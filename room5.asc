// room script file

bool batteryAdded = false;
bool floodlightAnimationPlayed = false;

function hHotspot1_AnyClick(Hotspot *theHotspot, CursorMode mode)
{
  if(player == cArin) {
    player.Walk(159, 193, eBlock);
    player.ChangeRoom(3, 1271, 140);
  }
}

function oFloodLight_AnyClick(Object *theObject, CursorMode mode)
{
  player.Say("That's one of our maintenance flood lights.");
  player.Say("It would probably be on right now had the power not gone out.");
  if (batteryAdded) { EnterCircuitEditor("FloodLight"); }
}

function oFloodLight_UseInv(Object *theObject, CursorMode mode)
{
  if (player.ActiveInventory == iBattery)
  {
    player.Say("Maybe this could be hotwired to this light?");
    EnterCircuitEditor("FloodLight");
    player.LoseInventory(iBattery);
    batteryAdded = true;
  }
}

function room_RepExec()
{ 
  if (player == cArin) {
    RemoveWalkableArea(2);
  }
  else {
    RestoreWalkableArea(2);
  }
}

function room_AfterFadeIn()
{
  if (FloodlightRepaired && !floodlightAnimationPlayed) {
    Wait(SecondsToLoops(2.0));
    EnvironmentLit = true;
    Wait(SecondsToLoops(0.5));
    EnvironmentLit = false;
    Wait(SecondsToLoops(0.2));
    EnvironmentLit = true;
    Wait(SecondsToLoops(0.2));
    EnvironmentLit = false;
    Wait(SecondsToLoops(0.1));
    EnvironmentLit = true;
    IndependentShadow = true;
    floodlightAnimationPlayed = true;
  }  
    
}

function oBrokenBreakerBox_AnyClick(Object *theObject, CursorMode mode)
{
  if (player == cArin) {
    cArin.Say("Looks like another breakerbox went down.");
    cArin.Say("Good thing that one wasn't important.");
  }
  else {
    player.Walk(240, 113, eBlock);
    EnterCircuitEditor("BrokenBreakerbox");
  }
}

function oBlockade_AnyClick(Object *theObject, CursorMode mode)
{
  cArin.Say("Looks like part of the ship's interior frame has fallen down.");
  cArin.Say("Despite how easy it looks to climb over, it's not.");
  cArin.Say("End of discussion.");
}

function room_Load()
{
  //cArin.SetAsPlayer();
  if (cShadow.Room != 5) cShadow.ChangeRoom(5, 155, 120);
}

function oFuse_AnyClick(Object *theObject, CursorMode mode)
{
  AddInventoryHelper(theObject, iFuse);
  cArin.Say("This fuse looks to still be functional.");
}

function hArinCantWalkHere_AnyClick(Hotspot *theHotspot, CursorMode mode)
{
  cArin.Say("Looks like part of the ship's interior frame has fallen down.");
  cArin.Say("Despite how easy it looks to climb over, it's not.");
  cArin.Say("End of discussion.");
}


function hShadowDisappear_WalkOn(Hotspot *theHotspot)
{
  if (player == cShadow && FuseObtained) {
    cShadow.LoseInventory(iFuse);
    oFuse.X = cShadow.x - 50; // CHANGE THIS WHEN I MAKE FUSE SPRITES
    oFuse.Y = cShadow.y;
    oFuse.Visible = true;
    EnvironmentLit = false;
    IndependentShadow = false;
    cArin.SetAsPlayer();
    Wait(SecondsToLoops(2.0));
    cArin.Say("That battery didn't last very long...");
    cArin.Say("Better get this power back on quick.");
  }
}

function hShadowDisappear_AnyClick(Hotspot *theHotspot, CursorMode mode)
{
  return;
}
