// main global script file

//=============================================================================
// INITIALIZATION
//=============================================================================

// set default game options
function set_default_options()
{
  Game.TextReadingSpeed = 7;
  Game.MinimumTextDisplayTimeMs = 2000;
  System.Volume = 100;
  sldAudio.Value = System.Volume;
  sldSpeed.Value = 60;
  SetGameSpeed(60);
  CursorMode cursorMode = eModeWalkto;
}

// initialize gPanel
function initialize_control_panel()
{
  set_default_options();
}

// called when the game starts, before the first room is loaded
function game_start()
{
  // initialize gPanel controls
  initialize_control_panel();

  // set KeyboardMovement movement mode
  KeyboardMovement.Mode = eKeyboardMovementModeTapping;

  // set KeyboardMovement keys
  //KeyboardMovement.KeyUp = eKeyW;
  //KeyboardMovement.KeyDown = eKeyS;
  //KeyboardMovement.KeyLeft = eKeyA;
  //KeyboardMovement.KeyRight = eKeyD;

  // Auto-save on the save slot 999
  SetRestartPoint();
  
  Music = aSpace_Birds_V1.Play();
}

//=============================================================================
// Common GUI functions
//=============================================================================

// hide the icon bar and show a GUI
function open_gui(GUI* gui_to_open)
{
  if (gui_to_open != gInventory)
  {
    lblOverHotspot.Visible = false;
  }

  //mouse.UseModeGraphic(eModePointer);
  gui_to_open.Visible = true;
}

// hide the GUI and show the icon bar
function close_gui(GUI* gui_to_close)
{
  gui_to_close.Visible = false;
  //mouse.Mode = eModeWalkto;
  lblOverHotspot.Visible = true;
}

// hide a GUI, based on a GUI control
function close_owning_gui(GUIControl* control)
{
  close_gui(control.OwningGUI);
}

// hide a GUI directly from an OnClick event
function close_gui_onclick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
}

function show_inventory_window()
{
  if (player.ActiveInventory == null) mouse.Mode = eModeInteract;
  open_gui(gInventory);
}

function show_save_game_dialog()
{
  // get the list of save games
  lstSaveGamesList.FillSaveGameList();

  if (lstSaveGamesList.ItemCount > 0)
  {
    // if there is at least one, set the default text
    // to be the first game's name
    txtNewSaveName.Text = lstSaveGamesList.Items[0];
  }
  else
  {
    // no save games yet, so default to empty text
    txtNewSaveName.Text = "";
  }

  open_gui(gSaveGame);
}

function show_restore_game_dialog()
{
  lstRestoreGamesList.FillSaveGameList();
  open_gui(gRestoreGame);
}

//=============================================================================
// ICONBAR
//=============================================================================

//=============================================================================
// INVENTORY WINDOW
//=============================================================================

//=============================================================================
// CONTROL PANEL
//=============================================================================

function btnSave_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  Wait(1);
  show_save_game_dialog();
}

function btnQuit_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  open_gui(gExitGame);
}

function btnLoad_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  Wait(1);
  show_restore_game_dialog();
}

function sldAudio_OnChange(GUIControl *control)
{
  System.Volume = sldAudio.Value;
}

function btnDefault_OnClick(GUIControl *control, MouseButton button)
{
  set_default_options();
}

function sldSpeed_OnChange(GUIControl *control)
{
  SetGameSpeed(sldSpeed.Value);
}

//=============================================================================
// SAVE / LOAD DIALOGS
//=============================================================================

int find_save_slot(String name)
{
  bool slots[] = new bool[999];
  int slots_used = 0;

  // record which slots are occupied already, 
  // if the types save name matches any existing one, then use that
  for (int i = 0; i < lstSaveGamesList.ItemCount; i++)
  {
    if (lstSaveGamesList.Items[i] == name)
    {
      // found existing save with matching name
      return lstSaveGamesList.SaveGameSlots[i];
    }

    // remember which slots are already taken
    slots[lstSaveGamesList.SaveGameSlots[i]] = true;
    slots_used++;
  }
  
  // current version of AGS has a limit of 50 save slots
  // that may be displayed in the ListBox at the same time
  if (slots_used >= 50)
  {
    return -1;
  }

  // find first free save slot, starting with slot 1 (for "cosmetic" purposes)
  for (int i = 1; i < 999; i++)
  {
    if (!slots[i])
    {
      return i;
    }
  }

  // no free slots found
  return -1;
}

function btnSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  int gameSlotToSaveInto = find_save_slot(txtNewSaveName.Text);

  if (gameSlotToSaveInto < 0)
  {
    Display("Save slots limit of 50 is reached, delete some of the existing saves first!");
  }
  else
  {
    SaveGameSlot(gameSlotToSaveInto, txtNewSaveName.Text);
    close_owning_gui(control);
  }
}

function btnRestoreGame_OnClick(GUIControl *control, MouseButton button)
{
  if (lstRestoreGamesList.SelectedIndex >= 0)
  {
    RestoreGameSlot(lstRestoreGamesList.SaveGameSlots[lstRestoreGamesList.SelectedIndex]);
  }

  close_owning_gui(control);
}

function lstSaveGamesList_OnSelectionCh(GUIControl *control)
{
  txtNewSaveName.Text = lstSaveGamesList.Items[lstSaveGamesList.SelectedIndex];
}

function txtNewSaveName_OnActivate(GUIControl *control)
{
  // pressing Return in the text box simulates clicking the save button
  btnSaveGame_OnClick(control, eMouseLeft);
}

function btnDeleteSave_OnClick(GUIControl *control, MouseButton button)
{
  if (lstSaveGamesList.SelectedIndex >= 0)
  {
    DeleteSaveSlot(lstSaveGamesList.SaveGameSlots[lstSaveGamesList.SelectedIndex]);
    lstSaveGamesList.FillSaveGameList();
  }
}

//=============================================================================
// RESTART DIALOG
//=============================================================================

function btnRestart_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  open_gui(gRestart);
}

function btnRestartYes_OnClick(GUIControl *control, MouseButton button)
{
  RestartGame();
}

//=============================================================================
// QUIT DIALOG
//=============================================================================

function btnConfirmedQuit_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}

//=============================================================================
// PLAYER INPUT
//=============================================================================

// called when a key is pressed
function on_key_press(eKeyCode keycode, int mod)
{
  // check for Escape first, so it can be used on GUIs that pause the game
  if (keycode == eKeyEscape)
  {
    if (gPanel.Visible)
    {
      // Escape will turn the panel off
      close_gui(gPanel);
    }
    else if (gSaveGame.Visible)
    {
      // Escape will close the save game dialog
      close_gui(gSaveGame);
    }
    else if (gRestoreGame.Visible)
    {
      // Escape will close the restore game dialog
      close_gui(gRestoreGame);
    }
    if (gRestart.Visible)
    {
      // Escape will cancel a restart
      close_gui(gRestart);
    }
    else if (gExitGame.Visible)
    {
      // Escape will cancel exiting the game
      close_gui(gExitGame);
    }
    else
    {
      // Escape will turn the panel on
      open_gui(gPanel);
    }
  }
  else if (IsGamePaused() || !IsInterfaceEnabled())
  {
    // game paused, so don't react to any keypresses
    keycode = 0;
  }
  else if (keycode == eKeyQ && (mod & eKeyModCtrl))
  {
    // Ctrl-Q will quit the game
    open_gui(gExitGame);
  }
  else if (keycode == eKeyF5)
  {
    // F5 will prompt to save a game
    show_save_game_dialog();
  }
  else if (keycode == eKeyF7)
  {
    // F7 will prompt to save a game
    show_restore_game_dialog();
  }
  else if (keycode == eKeyF9)
  {
    // F9 will prompt to restart the game
    open_gui(gRestart);
  }
  else if (keycode == eKeyF12)
  {
    // F12 will save a screenshot to the save game folder
    SaveScreenShot("screenshot.pcx");
  }
  else if (keycode == eKeyReturn)
  {
    if (gRestart.Visible)
    {
      // Enter confirms a restart
      RestartGame();
    }
  }
  else if (keycode == eKeyTab)
  {
    // Tab opens the inventory
    show_inventory_window();
  }
  else if (mod & eKeyModCtrl)
  {
    if (keycode == eKeyS)
    {
      // Ctrl-S will give the player all defined inventory items
      Debug(0, 0);
    }
    else if (keycode == eKeyV)
    {
      // Ctrl-V will show game engine version and build date
      Debug(1, 0);
    }
    else if (keycode == eKeyA)
    {
      // Ctrl-A will show walkable areas
      Debug(2, 3);
    }
    else if (keycode == eKeyX)
    {
      // Ctrl-X will let the player teleport to any room
      Debug(3, 0);
    }
  }
}

function handle_room_click(MouseButton button)
{
  if (button == eMouseLeft)
  {
    // left-click, so try using the current mouse cursor mode at this position
    Room.ProcessClick(mouse.x, mouse.y, mouse.Mode );
  }
  else if (button == eMouseRight){
    mouse.Mode = eModeWalkto;
    player.ActiveInventory = null;
  }
  else if (button == eMouseMiddle)
  {
    // middle-click makes the character walk to clicked area, regardless of cursor mode
    Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
  }
  else if (button == eMouseWheelNorth)
  {
    // mouse wheel up will cycle the cursor mode backwards
    //mouse.SelectPreviousMode();
  }
}

function handle_inventory_click(MouseButton button)
{
  // InventoryItem.GetAtScreenXY could return null here
  // so using game.inv_activated instead is a safer option
  InventoryItem* item = inventory[game.inv_activated];
  
  if (button == eMouseLeftInv)
  {
    if (mouse.Mode == eModeInteract)
    {
      // interact mode selects an inventory item
      player.ActiveInventory = item;
    }
    else if (mouse.Mode == eModeUseinv)
    {
      if (item.ID != player.ActiveInventory.ID)
      {
        // use one item on another
        item.RunInteraction(eModeUseinv);
      }
    }
    else
    {
      // otherwise run corresponding interaction (LookAt, etc)
      item.RunInteraction(mouse.Mode);
    }
  }
  else
  {
    // right click is always "Look At"
    item.RunInteraction(eModeLookat);
  }
}

// called when a mouse button is clicked
function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeftInv || button == eMouseRightInv || button == eMouseMiddleInv)
  {
    handle_inventory_click(button);
  }
  // game is paused, then don't process mouse clicks inside the room
  else if (!IsGamePaused())
  {
    handle_room_click(button);
  }
}

void UpdateMouseGraphic() {
  if (player.ActiveInventory != null) return;
  int newGraphic = 29;  // no location / walking
  int newView = 6;
  CursorMode cursorMode = eModeWalkto;
  int lt = GetLocationType(mouse.x, mouse.y);
  if (lt == eLocationObject)
  {
    newGraphic = 30;
    newView = 5;
    cursorMode = eModeInteract;
  }
  if (lt == eLocationHotspot)
  {
    cursorMode = eModeInteract;
    // The cursor looks weird with how hotspots are used in the circuit editor room
    if (player.Room != 2) {
      newGraphic = 30;
      newView = 5;
    }
  }
  
    // interact
  else if (lt == eLocationCharacter)
  {
    if (Character.GetAtScreenXY(mouse.x, mouse.y) == cArin) return;
    newGraphic = 31;
    newView = 7;
    cursorMode = eModeTalkto;
  }
  // change?
  if (newGraphic != mouse.GetModeGraphic(mouse.Mode)) {
    mouse.ChangeModeGraphic(mouse.Mode, newGraphic);
    mouse.ChangeModeView(mouse.Mode, newView);
    mouse.Mode = cursorMode;
  }
}

void UpdateDoors() {
  Object* door = Object.GetAtScreenXY(mouse.x, mouse.y);
  // This is a dumb way to do this, but to close a door when you stop hovering, iterate through all doors in a room and try to close them
  if (door == null) {
    for (int i = 0; i < Room.ObjectCount; i++)
    {
      if(object[i].GetTextProperty("DoorType") == "Horizontal") {
        if (object[i].Graphic == 39) return;
        object[i].Graphic = 39;
      }
      else if (object[i].GetTextProperty("DoorType") == "Diagonal") {
        if (object[i].Graphic == 54) return;
        object[i].Graphic = 54;
      }
    }
    return;
  }
  if (door.GetTextProperty("DoorType") == "Horizontal") {
    if (door.GetProperty("DoorLocked")) return;
    if (door.Graphic == 38) return;
    door.Graphic = 38;
  }
  else if (door.GetTextProperty("DoorType") == "Diagonal") {
    if (!DoorRepaired) return; // This is a weird way to do this, if the starting door hasn't been repaired, don't open it on hover
    if (door.GetProperty("DoorLocked")) return;
    if (door.Graphic == 53) return;
    door.Graphic = 53;
  }
}

void CheckInventoryHotbar() {
  if (mouse.y > 180 && GetLocationType(mouse.x, mouse.y) != eLocationHotspot) {
    show_inventory_window();
  }
  else {
    if (gInventory.Visible == false) return;
    close_gui(gInventory);
  }
}

//=============================================================================
// GAME EVENTS
//=============================================================================

bool roomLit = true;

// called on every game cycle, except when the game is blocked
function repeatedly_execute()
{
  UpdateMouseGraphic();
  UpdateDoors();
  CheckInventoryHotbar();
}

// called on every game cycle, even when the game is blocked
function repeatedly_execute_always()
{
  if(IndependentShadow) {
    gShadowButton.Visible = true;
  }
  else
  {
    gShadowButton.Visible = false;
  }
  
  if (EnvironmentLit)
  {
    // Only run the tint screen function once, this should help performance, but I didn't test performance without it
    if (!roomLit) 
    {
    TintScreen(0, 0, 0);
    cShadow.on = 1;
    roomLit = true;
    }
  }
  else
  {
    if (roomLit) {
      TintScreen(1, 1, 1);
      cShadow.on = 0;
      roomLit = false;
    }
  }
}

// Called when a dialog script line "run-script" is processed
function dialog_request(int param)
{
}

// called when processclick wasn't handled anywhere else
function unhandled_event (int what, int type)
{
  if(player.ScriptName == "cShadow") { return; }
  if (what == 1) // Unhandled events for HOTSPOTS
  {
    if (type == 1) // look
    {
      player.Say("I see nothing special about it.");
    }
    else if (type == 2) // interact
    {
      player.Say("I can't do anything with it.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That won't do anything.");
    }
    else if (type == 4) // talk to
    {
      player.Say("I don't think it's going to talk back.");
    }
    else if (type == 7) // pick up
    {
      player.Say("I'm not sure I should be taking it.");
    }
  }
  else if (what == 2) // Unhandled events for OBJECTS
  {
    if (type == 0) // look
    {
      player.Say("Looks alright.");
    }
    else if (type == 1) // interact
    {
      player.Say("I'm not sure how to use it.");
    }
    else if (type == 2) // talk to
    {
      player.Say("I don't think it's going to answer me.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That's a funny idea.");
    }
    else if (type == 5) // pick up
    {
      player.Say("I don't want to have it.");
    }
  }
  else if (what == 3) // Unhandled events for CHARACTERS
  {
    if (type == 0) // look
    {
      player.Say("Hm.");
    }
    else if (type == 1) // interact
    {
      player.Say("Eh?");
    }
    else if (type == 2) // talk to
    {
      player.Say("Got nothing to say.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("I don't think I should give that away.");
    }
    else if (type == 5) // pick up
    {
      player.Say("I'm not sure they would be compliant.");
    }
  }
  else if (what == 5) // Unhandled events for INVENTORY ITEMS
  {
    if (type == 0) // look
    {
      player.Say("It's just some junk in my inventory.");
    }
    else if (type == 1) // interact
    {
      player.Say("Er, no?");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That's ridiculous.");
    }
  }
}

//=============================================================================
// Global interactions: Characters, Inventory items, ...
//=============================================================================

function AddInventoryHelper(Object *pickup, InventoryItem *invItem)
{
  player.Walk(pickup.X, pickup.Y, eBlock);
  pickup.Visible = false;
  player.AddInventory(invItem);
  aPickup.Play();
  String obtainedMessage = invItem.Name;
  obtainedMessage = obtainedMessage.Append(" Obtained");
  Display(obtainedMessage);
}

function EnterCircuitEditor(String circuitID)
{
  player.ChangeRoom(2, 160, 245);
  playerXCache = player.x;
  playerYCache = player.y;
  CircuitID = circuitID;
}

function ShadowSelect_OnClick(GUIControl *control, MouseButton button)
{
  if (player == cArin) 
  {
    FadeOut(10);
    cShadow.SetAsPlayer();
    ShadowSelect.NormalGraphic = 28;
    FadeIn(10);
  }
  else
  {
    FadeOut(10);
    cArin.SetAsPlayer();
    ShadowSelect.NormalGraphic = 17;
    FadeIn(10);
  }
}

function cShadow_AnyClick(Character *theCharacter, CursorMode mode)
{
  if (OpeningStuck) {
    cArin.Say("Hello?");
    cArin.Say("Is someone there?");
    IndependentShadow = true;
  }
}

function gCircuitExit_OnClick(GUI *theGui, MouseButton button)
{
  player.ChangeRoom(player.PreviousRoom, playerXCache, playerYCache);
}
