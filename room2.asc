// room script file

ParticleDefinition* GetSparkParticle()
{
  ParticleDefinition* sparkParticle = new ParticleDefinition;
  sparkParticle.Sprite = 18;
  sparkParticle.LifeTotal = 100;
  sparkParticle.VelX = Random(1500) - 1000;
  sparkParticle.VelY = Random(1500) - 1000;
  sparkParticle.TransparencyBegin = 0;
  sparkParticle.TransparencyEnd = 100;
  sparkParticle.WidthBegin = 1;
  sparkParticle.HeightBegin = 1;
  sparkParticle.Gravity = 100;
  sparkParticle.GroundY = 75;
  sparkParticle.Bounces = true;
  return sparkParticle;
}

Emitter sparkEmitter;

bool previousRoomEnvironmentLitCache;
int playerXCache;
int playerYcache;
String tutorialID;

bool tutorialPlayedWiring = false;

bool brokenBreakboxFuseTaken = false;

// Whether the sparking particle emitter should be firing every 2 seconds
bool sparking = false;

function room_AfterFadeIn()
{
  if (DoorRepaired) gCircuitExit.Visible = true; // Don't allow the player to exit the circuit GUI until they have figured out the first puzzle
  PlayTutorial(tutorialID);
  tutorialID = "";
}

int loopIterator = 0;
function repeatedly_execute_always()
{
  // Spark every x seconds if sparking == true
  sparkEmitter.Update();
  if (sparking && loopIterator >= SecondsToLoops(2.0)) {
    sparkEmitter.Emit();
    loopIterator = 0;
  }
  loopIterator++;
}

function room_Leave()
{
  gCircuitExit.Visible = false;
  player.ActiveInventory = null;
  player.UnlockView();
  mouse.Mode = eModeWalkto;
  EnvironmentLit = previousRoomEnvironmentLitCache;
}

function oCapacitor_UseInv(Object *theObject, CursorMode mode)
{
  // Player repairs circuit
  if (player.ActiveInventory == iCapacitor)
  {
    player.Say("This should fix it!");
    player.LoseInventory(iCapacitor);
    oDoorControllerCapacitor.Name = "Capacitor";
    oDoorControllerIndicator.Graphic = 51;
    DoorRepaired = true;
    sparking = false;
    gCircuitExit.Visible = true;
  }
}

function room_Load()
{
  // Particle Init
  int defs_count = 2048;
  ParticleDefinition *defs[] = new ParticleDefinition[defs_count];
  for(int i=0; i<defs_count; i++)
  {
    defs[i] = GetSparkParticle();
  }
  sparkEmitter.Init(187, 59, defs, defs_count, 32,  64);
  
  // Player/Room setup
  previousRoomEnvironmentLitCache = EnvironmentLit;
  EnvironmentLit = true;
  sparking = false;
  lblCircuitTip.Text = "";
  player.LockView(3, eKeepMoving);
  //player.Move(160, 245, eNoBlock, eAnywhere);
  
  LoadCircuit(CircuitID);
  
  // Setup Room based on CircuitID called
  switch (CircuitID)
  {
    case "DoorController":
      if (!DoorRepaired) { 
        sparking = true; 
        tutorialID = "Editor";
      }
      break;
    case "BreakerBox":
      break;
    case "CommunicationConsole":
      cArin.Say("I don't think I should be picking around in the communication console. It's kind of important.");
      break;
    case "BrokenConsole":
      break;
    case "FloodLight":
      if (!tutorialPlayedWiring) {
        tutorialID = "Wiring";
        tutorialPlayedWiring = true;
      }
      break;
    case "BrokenBreakerbox":
      oBreakerBox.Visible = true;
      if (!brokenBreakboxFuseTaken) oBrokenBreakerBoxFuse.Visible = true;
      break;
  }
}
function oBreakerBoxFuse_AnyClick(Object *theObject, CursorMode mode)
{
  player.Say("Looks like the breaker box's fuse is blown!");
  player.Say("Surely I can find another one somewhere around the ship.");
}

function oBreakerBoxFuse_UseInv(Object *theObject, CursorMode mode)
{
  if (player.ActiveInventory == iFuse)
  {
    player.Say("Let's get this fuse replaced.");
    theObject.Graphic = 43;
    player.LoseInventory(iFuse);
    player.Say("That should at least get the lights back on!");
    EnvironmentLit = true;
  }
}

function oCapacitor_Interact(Object *theObject, CursorMode mode)
{
  if (!DoorRepaired) 
    {
    cArin.Say("Looks to be blown.");
    cArin.Say("Probably what's keeping the door closed.");
    cArin.Say("I think I might have a spare in my pocket!");
    }
  else {
    cArin.Say("No more sparking.");
    cArin.Say("Huge improvement!");
  }
}

function oDoorControllerIndicator_AnyClick(Object *theObject, CursorMode mode)
{
  if (!DoorRepaired) 
    {
    cArin.Say("The indicator light is indicating the controller is broken.");
    cArin.Say("At least that's probably what red means.");
    }
  else {
    cArin.Say("Looks to be fixed!");
  }
}

function hDoorController_AnyClick(Hotspot *theHotspot, CursorMode mode)
{
  cArin.Say("This is the controller board for the door.");
}

// Connection point spark handler
function on_mouse_click(MouseButton button) {
  if (button != eMouseLeft) return;
  if (player.ActiveInventory != iWire) return;
  
  Object* connectionPointObject = Object.GetAtScreenXY(mouse.x, mouse.y);
  if (connectionPointObject == null) return;
  
  if(connectionPointObject.GetProperty("ConnectionPoint") != -1) {
    sparkEmitter.SetPosition(mouse.x, mouse.y);
    sparkEmitter.Emit();
  }
}

function oBrokenBreakerBoxFuse_AnyClick(Object *theObject, CursorMode mode)
{
  player.AddInventory(iFuse);
  oBrokenBreakerBoxFuse.Visible = false;
  brokenBreakboxFuseTaken = true;
  FuseObtained = true;
}


function oBattery_AnyClick(Object *theObject, CursorMode mode)
{
  player.Say("This battery should be able to power this flood light, as long as I have some wire to connect it.");
}
