// room script file

bool previousRoomEnvironmentLitCache;
int playerXCache;
int playerYcache;

function room_AfterFadeIn()
{
  if (DoorRepaired) {
    gCircuitExit.Visible = true;
  }
}

function room_Leave()
{
  gCircuitExit.Visible = false;
  player.ActiveInventory = null;
  player.UnlockView();
  mouse.Mode = eModeWalkto;
  EnvironmentLit = previousRoomEnvironmentLitCache;
}

function oCapacitor_UseInv(Object *theObject, CursorMode mode)
{
  if (player.ActiveInventory == iCapacitor)
  {
    player.Say("This should fix it!");
    theObject.Graphic = 14;
    player.LoseInventory(iCapacitor);
    DoorRepaired = true;
    gCircuitExit.Visible = true;
  }
}

function room_Load()
{
  previousRoomEnvironmentLitCache = EnvironmentLit;
  EnvironmentLit = true;
  player.LockView(3, eKeepMoving);
  player.Move(160, 245, eNoBlock, eAnywhere);
  
  for (int i = 0; i < Room.ObjectCount; i++)
  {
    object[i].Visible = false;
  }
  
  switch (CircuitID)
  {
    case "DoorController":
      oCapacitor.Visible = true;
      break;
    case "BreakerBox":
      oBreakerBox.Visible = true;
      oBreakerBoxFuse.Visible = true;
      break;
  }
}

function oBreakerBoxFuse_AnyClick(Object *theObject, CursorMode mode)
{
  player.Say("Looks like the breaker box's fuse is blown!");
  player.Say("Surely I can find another one somewhere around the ship.");
}

function oBreakerBoxFuse_UseInv(Object *theObject, CursorMode mode)
{
if (player.ActiveInventory == iFuse)
  {
    player.Say("Let's get this fuse replaced.");
    theObject.Graphic = 43;
    player.LoseInventory(iFuse);
    player.Say("That should at least get the lights back on!");
  }
}

function oCapacitor_Interact(Object *theObject, CursorMode mode)
{
  if (!DoorRepaired) 
    {
    player.Say("Looks to be blown.");
    player.Say("Probably what's keeping the door closed.");
    player.Say("I think I might have a spare in my pocket!");
    }
}
